# booth_bose.ode
# Pinsky-Rinzel pyramidal cell 
# simplified sd_inf and tau_sd = constant
# simplified cd_inf and tau_cd
# 
# parameter values
par ip10=0.75 ip1p=0.0 ip1on=310 ip1off=315
par gLs=0.1  gLd=0.1  gNa=30  gKdr=15  gCa=10  gKahp=0.8  gKC=15  
par VNa=60  VCa=80  VK=-75  VL=-60  Vsyn=0  
par gc=2.1  pp=0.5  Cm=3  
par tausd=2.535  
par taucd_c=2.5  taucd_sl=0.2
par betaqd=0.001  sl_betacd=27.0  
# equations for pyramidal cell 1
Vs1'=(-gLs*(Vs1-VL)-gNa*(Minfs(Vs1)^2)*hs1*(Vs1-VNa)-gKdr*ns1*(Vs1-VK)+(gc/pp)*(Vd1-Vs1)+Ip1/pp)/Cm
Vd1'=(-gLd*(Vd1-VL)-ICad1-gKahp*qd1*(Vd1-VK)-gKC*cd1*chid1*(Vd1-VK)+(gc*(Vs1-Vd1))/(1.0-pp))/Cm
Cad1'=  -0.13*ICad1-0.075*Cad1
hs1'=  alphahs(Vs1)-(alphahs(Vs1)+betahs(Vs1))*hs1
ns1'=  alphans(Vs1)-(alphans(Vs1)+betans(Vs1))*ns1
#sd1'=  alphasd(Vd1)-(alphasd(Vd1)+betasd(Vd1))*sd1
sd1'=  (sdinf(Vd1)-sd1)/tausd
cd1'=  (cdinf(Vd1)-cd1)/taucd(Vd1)
qd1'=  alphaqd1-(alphaqd1+betaqd)*qd1
# pyramidal cell functions
ICad1=     gCa*sd1*sd1*(Vd1-VCa)
alphams(v)=  0.32*(-46.9-v)/(exp((-46.9-v)/4.0)-1.0)
betams(v)=   0.28*(v+19.9)/(exp((v+19.9)/5.0)-1.0)
Minfs(v)=    alphams(v)/(alphams(v)+betams(v))
alphans(v)=  0.016*(-24.9-v)/(exp((-24.9-v)/5.0)-1.0)
betans(v)=   0.25*exp(-1.0-0.025*v)
alphahs(v)=  0.128*exp((-43.0-v)/18.0)
betahs(v)=   4.0/(1.0+exp((-20.0-v)/5.0))
#alphasd(v)=  1.6/(1.0+exp(-0.072*(v-5.0)))
#betasd(v)=   0.02*(v+8.9)/(exp((v+8.9)/5.0)-1.0)
sdinf(v)=1.0/(1.0+exp(-(v+20.0)/8))
#tausd(v)=0.6+5.7/((1.0+exp(-(v+12)/tausup))*(1.0+exp((v+12.0)/tausdown)))
#alphacd(v)=(1.0-heav(v+10.0))*exp((v+50.0)/11-(v+53.5)/27)/18.975+heav(v+10.0)*2.0*exp((-53.5-v)/27.0) 
#betacd(v)=   (1.0-heav(v+10.0))*(2.0*exp((-53.5-v)/sl_betacd)-alphacd(v))
cdinf(v)=(1.0-heav(v+10.0))*(1.0/(1.0+exp(-(v+10.0)/11)))+heav(v+10.0)
taucd(v)=(1.0-heav(v+10.0))*taucd_c+heav(v+10.0)*(taucd_sl*(v+10.0)+taucd_c)
alphaqd1=  min(0.00002*Cad1,0.01)
#betaqd=   0.001
chid1=     min(Cad1/250.0,1.0)
Ip1=ip10+ip1p*0.5*(1.0+tanh(t-ip1on))*0.5*(1.0+tanh(ip1off-t))
# initial conditions
init Vs1=0
# post-synaptic currents
aux Ikc=-gKC*cd1*chid1*(Vd1-VK)
aux Ikahp=-gKahp*qd1*(Vd1-VK)
aux ICad=ICad1
#
@ maxstor=80000,total=1000,bound=10000,xhi=1000,ylo=-70,yhi=30
@ meth=cvode,atol=0.0001,toler=0.0001,dt=0.3
done



